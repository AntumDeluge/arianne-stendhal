; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define RootDir "..\.."
#define MyAppName "Stendhal"
#define MyAppVersion "1.32.5"
#define MyAppPublisher "Faiumoni e. V."
#define MyAppURL "https://stendhalgame.org/"
#define MyAppExeName "stendhal-starter.exe"
#define MyCurrentYear GetDateTimeString('yyyy', '', '')

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{27ABFD93-94C1-4605-9005-6796CBE1102A}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
AppCopyright="Copyright (C) {#MyCurrentYear} {#MyAppPublisher}"
DefaultDirName={autopf}\{#MyAppName}
DisableProgramGroupPage=yes
LicenseFile="{#RootDir}\LICENSE.txt"
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
PrivilegesRequiredOverridesAllowed=dialog
OutputDir="{#RootDir}\build"
OutputBaseFilename="stendhal-{#MyAppVersion}-setup"
Compression=lzma
SolidCompression=yes
WizardStyle=modern
WizardImageFile="left.bmp"
WizardSmallImageFile="small.bmp"
VersionInfoVersion="{#MyAppVersion}"
VersionInfoDescription="Stendhal Client Setup"
SetupIconFile="..\launch4j\icon.ico"

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: "{#RootDir}\build\stendhal-{#MyAppVersion}\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#RootDir}\buildtools\launch4j\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Code]
#define JavaMin "1.8.0"
#define JavaURL "https://www.java.com/"

function JavaInstalled: Boolean;
var
	JavaExe: String;
begin
	Result := False;
	{TODO: check registry for compatible installed JREs}
	{Result := not RegKeyExists(HKEY_LOCAL_MACHINE, 'Software\Microsoft');}
	{TODO: check JAVA_HOME env variable}
	if not Result then
	begin
		{check PATH}
		{TODO: get java version from executable}
		JavaExe := FileSearch('java.exe', GetEnv('PATH'));
		Result := (JavaExe <> '');
	end;
end;

procedure InstallJava;
var
	ErrorCode: Integer;
begin
	{TODO: download & install Java automatically}
	ShellExec('open', '{#JavaURL}', '', '', SW_SHOW, ewNoWait, ErrorCode);
end;

function InitializeSetup: Boolean;
begin
	Result := True;
	if not JavaInstalled then
	begin
		if MsgBox('This program requires Java version 1.8.0 or newer to run but a suitable runtime was not found. Would you like to download Java?', mbConfirmation, MB_YESNO) = idYes then
		begin
			Result := False;
			MsgBox('The installer will now close & Java''s home page ( {#JavaURL} ) will be opened. Run this installer again after Java is set up.', mbInformation, MB_OK);
			InstallJava;
		end else begin
			MsgBox('WARNING: This program will not function without Java.', mbError, MB_OK);
		end;
	end;
end;

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

